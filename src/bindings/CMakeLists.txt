#check for swig if needed

cmake_policy(SET CMP0020 NEW)

IF (WITH_YAF_PY_BINDINGS OR WITH_YAF_RUBY_BINDINGS)
	
	FIND_PACKAGE(YafSWIG REQUIRED)
	
	IF(YAF_PY_VERSION GREATER 3.1)
		SET(YAF_SWIG_MIN_VERSION 2.0.4)
	ELSE(YAF_PY_VERSION GREATER 3.1)
		SET(YAF_SWIG_MIN_VERSION 1.3.36)
	ENDIF(YAF_PY_VERSION GREATER 3.1)
	
	IF(NOT SWIG_FOUND)
		MESSAGE("Swig not found, please install it to build script bindings")
		SET(WITH_YAF_PY_BINDINGS OFF)
		SET(WITH_YAF_RUBY_BINDINGS OFF)
	ELSE(NOT SWIG_FOUND)
		IF(SWIG_VERSION VERSION_LESS ${YAF_SWIG_MIN_VERSION})
			IF(YAF_PY_VERSION GREATER 3.1)
				message("Swig version < ${YAF_SWIG_MIN_VERSION}, to build with Python 3.2 you need an adecuately patched version of swig. Please set the path to your patched swig version in your UserConfig.txt file on the YAF_USER_SWIG_BIN_DIR variable. The patches required can be found on the SupportFiles folder on yafaray's source path.'")
			ELSE(YAF_PY_VERSION GREATER 3.1)
				message("Swig version < ${YAF_SWIG_MIN_VERSION}, please update or bindings can't be built")
			ENDIF(YAF_PY_VERSION GREATER 3.1)
			set(WITH_YAF_PY_BINDINGS OFF)
			set(WITH_YAF_RUBY_BINDINGS OFF)
		ENDIF(SWIG_VERSION VERSION_LESS ${YAF_SWIG_MIN_VERSION})
	ENDIF(NOT SWIG_FOUND)
	
	INCLUDE(${SWIG_USE_FILE})
ENDIF (WITH_YAF_PY_BINDINGS OR WITH_YAF_RUBY_BINDINGS)

# Python bindings 

IF (WITH_YAF_PY_BINDINGS)
	
	IF(YAF_PY_VERSION)
		set(REQUIRED_PYTHON_VERSION ${YAF_PY_VERSION})
	ELSE(YAF_PY_VERSION)
		set(REQUIRED_PYTHON_VERSION 3.5)
	endif(YAF_PY_VERSION)
	
	MESSAGE("Searching for Python ${REQUIRED_PYTHON_VERSION}")
	find_package(YafPythonLibs REQUIRED)
	
	if (NOT PYTHON_LIBRARIES)
		message("Python wasn't found, please install it to build python bindings")
		set(WITH_YAF_PY_BINDINGS OFF)
	else (NOT PYTHON_LIBRARIES)
		message("Python ${REQUIRED_PYTHON_VERSION} found at: ${PYTHON_LIBRARIES}")
	endif (NOT PYTHON_LIBRARIES)
	
endif (WITH_YAF_PY_BINDINGS)

# Ruby bindings

if (WITH_YAF_RUBY_BINDINGS)
	
	find_package(Ruby 1.8 REQUIRED)
	
	message("Ruby version found: ${RUBY_VERSION}")
	message("Ruby headers at: ${RUBY_INCLUDE_DIR}")
	message("Ruby library at: ${RUBY_LIBRARY}")

	if (NOT SWIG_FOUND OR NOT RUBY_FOUND)
		set(WITH_YAF_RUBY_BINDINGS OFF)
	endif (NOT SWIG_FOUND OR NOT RUBY_FOUND)
	
endif (WITH_YAF_RUBY_BINDINGS)


if (WITH_YAF_PY_BINDINGS)
	
	if(NOT WIN32)

		if(NOT YAF_BINDINGS_PY_DIR)
			set(YAF_BINDINGS_PY_DIR ${CMAKE_BINARY_DIR}/bindings/python)
		endif(NOT YAF_BINDINGS_PY_DIR)
		
	else(NOT WIN32)

		if(NOT YAF_BINDINGS_PY_DIR)
			set(YAF_BINDINGS_PY_DIR ${YAF_BIN_DIR}/python)
		endif(NOT YAF_BINDINGS_PY_DIR)

	endif(NOT WIN32)
	
	message("Python header path: ${PYTHON_INCLUDE_DIR}")
	include_directories(${YAF_INCLUDE_DIRS})
	include_directories(SYSTEM ${PYTHON_INCLUDE_DIR})
	include(${SWIG_USE_FILE})

	if(YAF_PY_VERSION GREATER 3)
		set(CMAKE_SWIG_FLAGS -c++ -py3)
	else(YAF_PY_VERSION GREATER 3)
		set(CMAKE_SWIG_FLAGS -c++)
	endif(YAF_PY_VERSION GREATER 3)

	set_source_files_properties(yafaray4_interface.i PROPERTIES CPLUSPLUS ON)

	if(WITH_QT)
		set_source_files_properties(yafaray4_qt_interface.i PROPERTIES CPLUSPLUS ON)
	endif(WITH_QT)

	SWIG_ADD_MODULE(yafaray4_interface PYTHON yafaray4_interface.i)

	if(APPLE) # use dynamic lookup for helping dlopen to find symbols properly - Jens
		SET_TARGET_PROPERTIES(${SWIG_MODULE_yafaray4_interface_REAL_NAME} PROPERTIES LINK_FLAGS "-undefined dynamic_lookup")
		SET(PYTHON_LIBRARIES "") #In MacOS, to avoid segfault 11 errors, do *NOT* link to any real python library, so it uses the undefined dynamic lookup to find the correct symbols.
	endif(APPLE)

	if(WITH_QT)
		SWIG_ADD_MODULE(yafaray4_qt_interface PYTHON yafaray4_qt_interface.i)
		if(APPLE) # use dynamic lookup for helping dlopen to find symbols properly - Jens
			SET_TARGET_PROPERTIES(${SWIG_MODULE_yafaray4_qt_interface_REAL_NAME} PROPERTIES LINK_FLAGS "-undefined dynamic_lookup")
		endif(APPLE)
	endif(WITH_QT)

	SWIG_LINK_LIBRARIES(yafaray4_interface libyafaray4 ${PYTHON_LIBRARIES})

	if(WITH_QT)
		SWIG_LINK_LIBRARIES(yafaray4_qt_interface yafaray4_qt ${PYTHON_LIBRARIES})
	endif(WITH_QT)

if(APPLE) # set rpath - Jens
	add_custom_command(TARGET ${SWIG_MODULE_yafaray4_interface_REAL_NAME} POST_BUILD COMMAND install_name_tool -add_rpath @loader_path/ _yafaray4_interface.so)
endif(APPLE)

	install(TARGETS ${SWIG_MODULE_yafaray4_interface_REAL_NAME} DESTINATION ${YAF_BINDINGS_PY_DIR})
	install(FILES ${CMAKE_CURRENT_BINARY_DIR}/yafaray4_interface.py DESTINATION ${YAF_BINDINGS_PY_DIR})

	if(WITH_QT)
		install(TARGETS ${SWIG_MODULE_yafaray4_qt_interface_REAL_NAME} DESTINATION ${YAF_BINDINGS_PY_DIR})
		install(FILES ${CMAKE_CURRENT_BINARY_DIR}/yafaray4_qt_interface.py DESTINATION ${YAF_BINDINGS_PY_DIR})
	endif(WITH_QT)

endif (WITH_YAF_PY_BINDINGS)

if (WITH_YAF_RUBY_BINDINGS)
	
	if(NOT WIN32)

		if(NOT YAF_BINDINGS_RUBY_DIR)
			set(YAF_BINDINGS_RUBY_DIR ${CMAKE_BINARY_DIR}/bindings/ruby)
		endif(NOT YAF_BINDINGS_RUBY_DIR)
		
	else(NOT WIN32)

		if(NOT YAF_BINDINGS_RUBY_DIR)
			set(YAF_BINDINGS_RUBY_DIR ${YAF_BIN_DIR}/ruby)
		endif(NOT YAF_BINDINGS_RUBY_DIR)

	endif(NOT WIN32)

	include_directories(${YAF_INCLUDE_DIRS})
	include_directories(SYSTEM ${RUBY_INCLUDE_DIR})
	include(${SWIG_USE_FILE})

	set(CMAKE_SWIG_FLAGS -c++ -w-801)

	set_source_files_properties(yafaray4_interface.i PROPERTIES CPLUSPLUS ON)
	SWIG_ADD_MODULE(yafaray4_interface_ruby RUBY yafaray4_interface.i)

	if(APPLE) # use dynamic lookup for helping dlopen to find symbols properly - Jens
		SET_TARGET_PROPERTIES(${SWIG_MODULE_yafaray4_interface_ruby_REAL_NAME} PROPERTIES LINK_FLAGS "-undefined dynamic_lookup")
		SET(RUBY_LIBRARY "") #In MacOS, to avoid Ruby "Incompatible version" errors, do *NOT* link to any real ruby library, so it uses the undefined dynamic lookup to find the correct symbols.
	endif(APPLE)

	SWIG_LINK_LIBRARIES(yafaray4_interface_ruby libyafaray4 ${RUBY_LIBRARY})
	install(TARGETS ${SWIG_MODULE_yafaray4_interface_ruby_REAL_NAME} DESTINATION ${YAF_BINDINGS_RUBY_DIR})

	if(WITH_QT)
		set_source_files_properties(yafaray4_qt_interface.i PROPERTIES CPLUSPLUS ON)
		SWIG_ADD_MODULE(yafaray4_qt_interface_ruby RUBY yafaray4_qt_interface.i)
		if(APPLE) # use dynamic lookup for helping dlopen to find symbols properly - Jens
			SET_TARGET_PROPERTIES(${SWIG_MODULE_yafaray4_qt_interface_ruby_REAL_NAME} PROPERTIES LINK_FLAGS "-undefined dynamic_lookup")
			SET(RUBY_LIBRARY "") #In MacOS, to avoid Ruby "Incompatible version" errors, do *NOT* link to any real ruby library, so it uses the undefined dynamic lookup to find the correct symbols.
		endif(APPLE)
		SWIG_LINK_LIBRARIES(yafaray4_qt_interface_ruby yafaray4_qt ${RUBY_LIBRARY})
		install(TARGETS ${SWIG_MODULE_yafaray4_qt_interface_ruby_REAL_NAME} DESTINATION ${YAF_BINDINGS_RUBY_DIR})
	endif(WITH_QT)

endif (WITH_YAF_RUBY_BINDINGS)

